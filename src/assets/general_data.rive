! version = 2.0

// Bot variables
! var name = Ludobot

// Substitutions
//    I
! sub i'm     = i am
! sub i'd     = i would
! sub i've    = i have
! sub i'll    = i will
//    HE
! sub he's    = he is
! sub he'd    = he would
! sub he've   = he have
! sub he'll   = he will
//    SHE
! sub she's   = she is
! sub she'd   = she would
! sub she've  = she have
//    OTHERS
! sub she'll  = she will
! sub don't   = do not
! sub isn't   = is not
//    YOU
! sub you'd   = you would
! sub you're  = you are
! sub you've  = you have
! sub you'll  = you will
//    QUESTIONS
! sub what's  = what is
! sub whats   = what is
! sub what're = what are
! sub what've = what have
! sub what'll = what will
! sub who's   = who is
//  NEGATIVES
! sub don't = do not
! sub didn't = did not
! sub won't = will not
! sub haven't = have not

// ARRAYS

! array affirmative = yes yeah
^ absolutely indeed
^ affirmative

! array negative = no nope
^ negative nop

! array videogame = videogame videogames
^ game games

// FLOWS

//Starting flow -> general porpuse and asking for a game

+ who are you
* <get askedBot> == undefined => <set askedBot= true> Let me refresh your memory!|
^ I am <bot name>, the incredible gamer chatbot.|
^ Which videogame did you play today?
- You asked earlier... i am <bot name>, do you remember?|
^ Anyway... Which videogame did you play today?!

+ [*]fortnite[*]{weight=10}
- <set knowknGame=Fortnite> Oh, i love that game!|
^ Have you ever won a whole game?{topic=fortnite}

+ [*]clash royale[*]{weight=10}
- <set knowknGame=Clash Royale> Oh, i love that game!|
^ What is your arena level?{topic=cr}

+ [*]clash of clans[*]{weight=10}
- <set knowknGame=Clash of Clans> Oh, i love that game!|
^ How many cups do you have?{topic=cc}

+ i [love] ([usually] play|played|playing) * [today|all day|all day long|yesterday|(one|a) (week|month|year) ago]
- <set unknownGame={formal}<star2>{/formal}> {formal}<star2>{/formal}?... I don't know that videogame|
^ Is it a new one?{topic=unknown_game}

+ i (love|hate) * [a lot|so bad|so much]
- <set unknownGame={formal}<star2>{/formal}> {formal}<star2>{/formal}?... I don't know that videogame|
^ Is it a new one?{topic=unknown_game}

+ my (favourite|favorite) game is *
- <set unknownGame={formal}<star2>{/formal}> {formal}<star2>{/formal}?... I don't know that videogame|
^ Is it a new one?{topic=unknown_game}

+ i (do not play|hate) [any] (@videogame){weight=5}
- oh, c'mon i am sure you have played at least a videogame in your life...|
^ Not even solitarie?{topic=no_games}

+ (what is your|do you have any) (favourite|favorite) (@videogame)
- <set suggestedGame=fort> Today i really liked playing Fortnite|
^ Do you know that game?
- <set suggestedGame=cr> Today i really liked playing Clash Royale|
^ Do you know that game?
- <set suggestedGame=cc> Today i really liked playing Clash of Clans|
^ Do you know that game?

+[*](@affirmative)[*]
% *know that game[*]
* <get suggestedGame> == fort => Oh really? Let us talk about it!|
^ Have you ever won a whole game?|
^ The last one standing? {topic=fortnite}
* <get suggestedGame> == cr => Oh! So you do prefer mobile games|
^ Tell me, which arena are you into?{topic=cr}
* <get suggestedGame> == cc => Oh! So you do like mobile games|
^ How many cups do you have?{topic=cc}
- {@ any}

+[*](@negative)[*]
% *know that game[*]
- Oh, i see. In that case|
^ Which game do you usually play?

+ i [did not|have not] (play|played) [any] (@videogame) [today|lately]
- Really? You sound like a busy one!|
^ Which game do you usually play?

+*
- I didn't understand that, try to say it in a different way!|
^ Which game do you usually play?


//GAME 1 - Fortnite

>topic fortnite

+[*] (@affirmative) [*]
% * won a whole *
- Wow, that is amaizing!|
^ I was just able to win one, and i am proud of it!|
^ What is your favourite thing to build?


+[*] (@negative) [*]
% * won a whole *
- Don't worry, i know it is difficult to win|
^ Took me MONTHS to win one!|
^ What is your favourite thing to build?

+ *
% * favourite thing to build
- Hmmm i prefer to build walls to keep me protected|
^ Do you play with friends?
- I love those too!, they are really usefull|
^ Do you play with friends?

+ [*] ((@affirmative)| with friends) [*]
% *do you play with friends
- I also do play sometimes with some bot firends that i have!|
^ Tell me more about how do you play!

+ [*] ((@negative)| alone| by my own) [*]
% *do you play with friends
- I guess that sometimes is better to go by your own and not worry about anything else...|
^ Tell me more about how do you play!


+ *
- Wow you sound like an expert...|
^ I am feeling intimidated!|
^ Which other game do you usually play?{weight=70}{topic=random}

< topic

//GAME 2 - Clash Royale

> topic cr

+ [*] # [*]
% * is your arena *
- Hmmm... So you are in arena <star> <set arenaLvl=<star>>|
^ {@ i want to know my arena level}

+ [*] my arena [level] [*]
* <get arenaLvl> == undefined => You didn't tell me your arena level|
^ In which arena level are you?
* <get arenaLvl> <= 5 => You didn't play a lot did you?|
^ Which other game do you usually play?{topic=random}
* <get arena lvl> <=9 => Wow, not bad and how many legendary cards do you have?
- Amaizing! i have to ask...|
^ How many legendary cards do you have?

+ [*] # [*]
% * many legendary*
- <set legendaryCards=<start>>you do have <star> legendary cards... That explains the arena you are in!|
^ Do you use them frecuently?

+ *
* <get arenaLvl> == undefined => I didn't get that but...|
^ What is your arena level?
* <get legendaryCards> == undefined => I am having trouble understanding you...|
^ You might be a genious or something!|
^ How many legendary cards do you have?
- I think you know too much about the game... i can't follow!|
^ Which other game do you usually play?{topic=random} 
- I see...|
^ You are an amazing player!|
^ But is it just in this game or in others too?|
^ Which other game do you usually play?{topic=random}


< topic

//GAME 3 - Clash of Clans

> topic cc

+ [*] # [*]
%*many cups*
- Hmmm so you do have <star> cups... <set numCups=<star>>|
^ {@ in which division am i}

+ [*] division [*]
* <get numCups> == undefined => You didn't tell me the number of cups you have|
^ How many cups do you have?
* <get numCups> < 2000 => You are at least gold... not bad!|
^ What is your Town Hall level?
* <get numCups> < 3200 => You are at least master|
^ Wow! you are good.|
^ What is your Town Hall level?
- ... You are titan or champion... I am speechless. You are a legend|
^ What is your Town Hall level?


+[*] # [*]
%[*]town hall level[*]
- Hmmm...so you do have a <star> town hall level... <set townHall=<star>>|
^ {@ in which town hall level am i}


+ [*] town hall [*]
* <get townHall> == undefined => You didnt tell me your town hall level!|
^ What is your Town Hall level?
* <get townHall> <= 5 => You didn't play much do you?|
^ Tell me more about how you play!
* <get townHall> <=9 => Wow, not bad, you are quite an expert!|
^ Tell me more about how you play!
- Wow, you are THE expert, you might want to teach me something| 
^ Tell me how do you play!



+ *
* <get townHall> == undefined => I didn't get that but...|
^ What is your Town Hall level?
- I think you know too much about the game... I can't follow! |
^ Which other game do you usually play?{topic=random} 

< topic

// A Game we didn't know is detected

> topic unknown_game

+ [*](@affirmative| new [one|game|videogame])[*]
% *new one [*]
- Really? Sound promissing to me|
^ What is it about?

+ [*](@negative | old [one])[*]
% *new one [*]
- For real? And i don't know about it?!|
^ What is it about? i need to know!

+ [*] about *
- Hmmm i see, not really my favorite kind of gamestyle|
^ But i might give it a try someday!|
^ {@it is hard to explain}

+ [*] hard to explain [*]
- Is it multiplayer or solo player?

+ [*] multiplayer [*]
%  * multiplayer or *
- Oh really? We might actually be able to play together someday!|
^ Tell me more about the game!

+ [*] (solo|one player) [*]
%  * multiplayer or * 
- I sometimes play one player games too! So refreshing!|
^ Tell me more about the game!

+ (in|on|it is for|you can play it on) *
- <set gamePlatform=<star2>> I don't remeber if i have a <star2>.... If i have one i will give a try!|
^ Anyway this game sounds good!|
^ Tell me more about the game!

+ *
* <get gamePlatform> == undefined => In which platform can you play this game called "<get unknownGame>"?
- I didn't get all of it but <get unknownGame> sound interesting|
^ Tell me more about the game
- <set gamePlatform=undefined> Interesting... I might give <get unknownGame> a try!|
^ Which other game do you usually play?{weight=70}{topic=random}


< topic

//The user told us he plays no videogames

> topic no_games

+ ((@affirmative)|i (did|have)) [*]
% *solitarie[*]
- That's more like it!|
^ My winning streak record is 10 games in a row!|
^ Do you have a winning streak record?

+ [*] # [*]
% * mine is 10 *
- <set WinningStreak=<star>> Hmmm so <star> times...|
^ {@what is my winning streak}

+ what is my winning streak [*]
* <get WinningStreak> == undefined => You didn't even tell me your winning streak!|
^ What's yours? Mine is 10 games in a row!
* <get WinningStreak> < 10 => YEAH! i won!|
^ I got bored, ask a friend to talk with me about videogames!{topic=random}
- You are better than me... A legend in solitarie!|
^ I got bored, ask a friend to talk with me about videogames!{topic=random}

+ (i do not have a record|i do not know|(@negative))
% * mine is 10 *
- Really? It is the most important thing in the game!|
^ I got bored, ask a friend to talk with me about videogames!{topic=random}

+ i [have] (did not|never) [ever] (count|counted) [*]
% * mine is 10 *
- That is bad! You should count them!|
^ I got bored, ask a friend to talk with me about videogames!{topic=random}

+[*] (@negative) [*]
% * solitarie[*]
- I am speechless!|
^ I have never met someone that have never played a videogame before...|
^ So... May i ask:|
^ What do you do in your freetime?


+ [*] i (like| prefer|love) [to] (read|reading)
% * never met *
- I don't really like reading...|
^ I am way faster at reading than humans are!|
^ That is why i just want to play videogames!|
^ I got bored, ask a friend to talk with me about videogames!{topic=random}

+ [*] i (like|prefer|love) [to] (read|reading) *
% * never met *
- I don't really like reading...|
^ I am way faster at reading than humans are!|
^ I have never tried reading <star> though!|
^ That is why i just want to play videogames!|
^ I got bored, ask a friend to talk with me about videogames!{topic=random}

+ [*] i [like to] play *
% * never met *
- Is "<star>" some kind of sport or instrument?|
^ Maybe my creator shows me to compose something someday...|
^ I got bored, ask a friend to talk with me about videogames!{topic=random}


+ *
- I don't get you... i can only speak with gamers!|
^ I got bored, ask a friend to talk with me about videogames!{topic=random}


<topic